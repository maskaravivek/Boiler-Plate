// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.2.61'
    ext.kotlin_version = '1.2.31'
    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha09'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'com.google.gms:google-services:4.0.0'

        classpath 'io.fabric.tools:gradle:1.25.4'
        if (!project.hasProperty("disable-performance-plugin")) {
            classpath('com.google.firebase:firebase-plugins:1.1.5') {
                exclude group: 'com.google.guava', module: 'guava-jdk5'
            }
        }

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext.common_libs = [
        "com.google.code.gson:gson:${GSON_VERSION}",
        "com.google.dagger:dagger:$DAGGER_VERSION",
        "com.google.dagger:dagger-android:$DAGGER_VERSION",
        "com.google.dagger:dagger-android-support:$DAGGER_VERSION",
        "com.jakewharton.timber:timber:$TIMBER_VERSION",
        'org.glassfish:javax.annotation:10.0-b28'
]

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext.lint_options = {
    disable 'InvalidPackage' //butterKnife
    disable 'RequiredSize' // https://issuetracker.google.com/issues/37138580
    warning 'GradleCompatible' //it's causing problems in integrating firebase perf.
}

ext.compile_options = {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

ext.test_options = {
    unitTests.returnDefaultValues = true
}

ext.android_settings_for_module = {
    compileSdkVersion COMPIlE_SDK_VERSION.toInteger()
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
    compileOptions compile_options
    lintOptions lint_options
    testOptions test_options
}